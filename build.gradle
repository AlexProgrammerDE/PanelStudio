
allprojects {
	group = 'com.lukflug'
	version = '0.1.8'
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
	
	task sourcesJar(type: Jar) {
		from sourceSets.main.allJava
		classifier = 'sources'
	}
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact jar
				artifact sourcesJar
				pom {
					url = 'https://github.com/lukflug/PanelStudio'
					licenses {
						license {
							name = 'MIT License'
							url = 'https://raw.githubusercontent.com/lukflug/PanelStudio/main/LICENSE'
						}
					}
					developers {
						developer {
							id = 'lukflug'
						}
					}
				}
			}
		}
		repositories {
			maven {
				url = "${projectDir}/../../lukflug.github.io/maven"
			}
		}
	}
}

task alljavadoc(type: Javadoc) {
	source = subprojects.collect{it.sourceSets.main.allJava}    											  
	classpath = files(subprojects.collect{it.sourceSets.main.runtimeClasspath})
	destinationDir = file("${buildDir}/docs/javadoc")
	title = "PanelStudio API Version ${version}"
}
